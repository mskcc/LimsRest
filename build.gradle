group 'org.mskcc.limsrest'
version '1.16.4-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

if (target== 'dev'){
    copy {
        from "src/dev/resources"
        into "src/main/resources"
    }
    version += 'd'
} else if(target == 'test'){
    copy {
        from "src/test-site-res/resources"
        into "src/main/resources"
    }
} else {
    copy {
        from "src/production/resources"
        into "src/main/resources"
    }
}

war{
    baseName = 'LimsRest##'
    if(target == 'test'){
       baseName = 'TestRest##'
    }
}

jar {
    baseName = 'lims-rest-service'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "${nexusUrlReleases}" }
    maven { url "${nexusUrlSnapshots}" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configurations {
    providedRuntime
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    providedRuntime ("org.springframework.boot:spring-boot-starter-tomcat")

    compile ("org.springframework.boot:spring-boot-starter-web")
    compile ("org.springframework.boot:spring-boot-starter-security")
    compile ('javax.servlet:jstl:1.2')
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile (group: 'com.google.guava', name: 'guava', version: '18.+')
    compile 'org.mskcc.common:common-domain:2.13.0-SNAPSHOT'
    compile 'com.velox.internalproducts:veloxapi:10.6.1-b777'
    compile 'com.velox.sloan.cmo:sloancmo:2.21.1-LIMS10.6.1_b84'

    compile 'com.velox.internalproducts:sapiogeneric:15.0.0-LIMS10.6.1'
    compile 'com.velox.internalproducts:sapioutils:15.0.0-LIMS10.6.1'
    compile 'com.velox.internalproducts:sapio-commons:15.0.0-LIMS10.6.1'
    compile 'com.velox.internalproducts:veloxapi:10.6.1-b777'

    compile fileTree (dir: 'libs', include: '*.jar')

    testCompile ('io.rest-assured:rest-assured:3.0.1')
    testCompile ("junit:junit")
    testCompile ("org.mockito:mockito-core:1.10.19")
    compile (group: 'org.springframework', name: 'spring-test', version: '4.1.7.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-test')
    compile('org.junit.jupiter:junit-jupiter:5.4.0')
    compile 'org.assertj:assertj-core:3.9.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

bootRun {
    if (System.getProperty('DEBUG', 'false') == 'true')
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
    systemProperty 'server.port', '5002'
}

tasks.withType(Test) {
    if (System.getProperty('DEBUG', 'false') == 'true')
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006'

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    description = "Integration Tests"
    outputs.upToDateWhen { false }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                    url: "${nexusUrlReleases}") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(
                    url: "${nexusUrlSnapshots}") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                parent {
                    groupId 'io.spring.platform'
                    artifactId 'platform-bom'
                    version '1.0.2.RELEASE'
                }
            }
        }
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
}
